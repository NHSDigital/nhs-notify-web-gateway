resource "aws_s3_bucket_policy" "static_assets" {
  bucket = aws_s3_bucket.static_assets.id
  policy = data.aws_iam_policy_document.static_assets_bucket_policy.json
}

data "aws_iam_policy_document" "static_assets_bucket_policy" {
  statement {
    actions = ["s3:GetObject"]
    resources = [
      "${aws_s3_bucket.static_assets.arn}/*"
    ]

    principals {
      type        = "AWS"
      identifiers = flatten([[local.static_assets_bucket_policy_identifiers], [aws_cloudfront_origin_access_identity.static_assets.iam_arn]])
    }
  }

  statement {
    actions = ["s3:ListBucket"]
    resources = [
      aws_s3_bucket.static_assets.arn
    ]

    principals {
      type        = "AWS"
      identifiers = flatten([[local.static_assets_bucket_policy_identifiers], [aws_cloudfront_origin_access_identity.static_assets.iam_arn]])
    }
  }

  statement {
    effect  = "Deny"
    actions = ["s3:*"]
    resources = [
      aws_s3_bucket.static_assets.arn,
      "${aws_s3_bucket.static_assets.arn}/*",
    ]

    principals {
      type        = "AWS"
      identifiers = ["*"]
    }

    condition {
      test     = "Bool"
      variable = "aws:SecureTransport"
      values = [
        false
      ]
    }
  }
}
